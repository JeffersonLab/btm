plugins {
    id 'war'
    id "com.diffplug.spotless" version "7.2.1"
}
description = 'Beam Time Manager app'
group = 'org.jlab'
version = new File("${projectDir}/VERSION").text.trim()
ext.version = project.version
ext.smoothnessVersion = '5.1.0'
ext.releaseDate = new Date().format('MMM dd yyyy')

tasks.withType(JavaCompile).configureEach {
    options.release = 17
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.epics:jca:2.4.10'

    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation "org.jlab:smoothness-weblib:${smoothnessVersion}"

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:5.1.3',
                    'org.jlab:jlog:5.2.0',
                    'org.apache.poi:poi:5.4.1',
                    'org.apache.poi:poi-ooxml:5.4.1'

    // Wildfly 37.0.1.Final provides:
    providedCompile 'jakarta.platform:jakarta.jakartaee-api:10.0.0',
            'org.hibernate:hibernate-core:6.6.19.Final',
            'org.hibernate:hibernate-envers:6.6.19.Final',
            'org.glassfish.web:jakarta.servlet.jsp.jstl:3.0.1'
}
tasks.named('jar') {
    enabled = false
}
war {
    archiveFileName = 'btm.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
        filter {
            String line -> line.replaceAll("@SMOOTHNESS_VERSION@", smoothnessVersion)
        }
    }
}

spotless {
    java {
        googleJavaFormat()
    }
}