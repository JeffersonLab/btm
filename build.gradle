plugins {
    id 'war'
}
description = 'Beam Time Manager app'
group 'org.jlab'
version '3.7.2'
ext.releaseDate = 'Aug 22 2023'
tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs += ["-Xlint:deprecation", "-Xlint:unchecked"]
}
repositories {
    mavenCentral()
}
dependencies {
    implementation 'org.epics:jca:2.4.2'

    // No matter what smoothness weblib MUST be included in WAR (https://github.com/JeffersonLab/smoothness/issues/4)
    implementation 'org.jlab:smoothness-weblib:4.3.0'

    // Smoothness setup should install these into Wildfly
    providedCompile 'org.tuckey:urlrewritefilter:4.0.4',
                    'org.jlab:jlog:5.0.0',
                    'org.apache.poi:poi:5.2.3',
                    'org.apache.poi:poi-ooxml:5.2.3'

    // Wildfly 26.1.3.Final provides:
    providedCompile 'javax:javaee-api:8.0.1',
                    'javax.servlet:jstl:1.2',
                    'org.hibernate:hibernate-core:5.3.28.Final',
                    'org.hibernate:hibernate-envers:5.3.28.Final'
}
war {
    archiveFileName = 'btm.war'
    filesMatching('WEB-INF/web.xml') {
        filter {
            String line -> line.replaceAll("@VERSION@", project.version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}
